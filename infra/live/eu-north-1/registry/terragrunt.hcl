include "root" {
  path   = find_in_parent_folders()
  expose = true
}

dependency "dev-compute" {
  config_path = "../dev/compute/terragrunt.hcl"

  mock_outputs = {
    eks_nodes_role_arn = "arn:aws:iam::012345678901:role/devops"
  }
}

dependency "prod-compute" {
  config_path = "../prod/compute/terragrunt.hcl"

  mock_outputs = {
    eks_nodes_role_arn = "arn:aws:iam::012345678901:role/devops"
  }
}

locals {
  apps = [
    "golang",
    "php"
  ]
}

generate "ecr_repos" {
  path      = "_generated_ecr_repos.tf"
  if_exists = "overwrite_terragrunt"
  contents  = <<EOF
# This file is dynamically generated by terragrunt.hcl

module "ecr_repository" {
  source   = "github.com/terraform-aws-modules/terraform-aws-ecr//?ref=v3.1.0"
  for_each = toset(${jsonencode(local.apps)})

  repository_name      = "app-$${each.key}-${include.root.locals.aws_region}-registry"
  repository_image_tag_mutability = "IMMUTABLE"

  repository_read_write_access_arns = [
    ${jsonencode(dependency.dev-compute.outputs.eks_nodes_role_arn)},
    ${jsonencode(dependency.prod-compute.outputs.eks_nodes_role_arn)}
  ]

  repository_lifecycle_policy = jsonencode({
    rules = [
      {
        rulePriority = 1,
        description  = "Keep last 30 images",
        selection = {
          tagStatus     = "tagged",
          tagPrefixList = ["v"],
          countType     = "imageCountMoreThan",
          countNumber   = 30
        },
        action = {
          type = "expire"
        }
      }
    ]
  })

  tags = {
    Terraform = "true"
    Name      = "app-$${each.key}-${include.root.locals.aws_region}-registry"
    App       = each.key
  }
}
EOF
}